buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'

group = 'com.github.johansonck'

sourceCompatibility = 1.8 // java 8
targetCompatibility = 1.8

dependencies {
    implementation('org.springframework.boot:spring-boot-starter')
    implementation('com.github.felixgail:gplaymusic:0.3.5')
    implementation('org.apache.commons:commons-lang3:3.8.1')
    implementation('com.google.code.gson:gson:2.7')

    compileOnly('org.projectlombok:lombok:1.18.2')

    testImplementation('junit:junit:4.12')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}
